plugins {
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id 'java'
    id 'maven-publish'
}

dependencies {
    implementation "com.googlecode.json-simple:json-simple:$jsonSimpleVersion"
    implementation "com.google.guava:guava:$guavaVersion"
    compileOnly "org.slf4j:slf4j-api:1.7.30"
    compile "com.github.egg82:DepDownloader:20373b7f6e"
    compile "me.lucko:jar-relocator:1.4"
}

ext.artifactName = 'platform-abstraction-layer-api'
ext.artifactVersion = project.version

publishing {
    repositories {
        maven {
            name = "plan-repo"
            url = uri("https://repo.playeranalytics.net/releases")
            credentials {
                username = "user"
                password = System.getenv("REPOSILITE_TOKEN")
            }
        }
    }
    publications {
        deployment(MavenPublication) {
            publication ->
                project.shadow.component(publication)
                groupId 'net.playeranalytics'
                artifactId "$artifactName"
                version "$artifactVersion"
        }
    }
}

shadowJar {
    archiveClassifier.set('')
    configurations = [project.configurations.compile]

    relocate 'ninja.egg82', 'net.playeranalytics.plugin.ninja.egg82'
    relocate 'me.lucko', 'net.playeranalytics.plugin.me.lucko'
}