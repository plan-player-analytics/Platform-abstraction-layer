allprojects {
    group "net.playeranalytics"
    version "5.2.0"
}

subprojects {
    apply(plugin: "java-library")
    apply(plugin: "maven-publish")

    tasks.withType(JavaCompile).configureEach {
        options.release = 11
        options.encoding = "UTF-8"
    }

    ext {
        paperVersion = '1.16.4-R0.1-SNAPSHOT'
        spongeVersion = '8.0.0-SNAPSHOT'
        nukkitVersion = '1.0-SNAPSHOT'
        bungeecordVersion = '1.16-R0.4'
        velocityVersion = '3.0.0-SNAPSHOT'
        foliaVersion = '1.20.1-R0.1-SNAPSHOT'
        jsonSimpleVersion = '1.1.1'
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    }

    test {
        useJUnitPlatform()
        testLogging {
            events 'passed', 'failed', 'skipped'
            exceptionFormat 'full'
        }
    }

    publishing {
        repositories {
            maven {
                name = "plan-repo"
                url = uri("https://repo.playeranalytics.net/releases")
                credentials {
                    username = "user"
                    password = System.getenv("REPOSILITE_TOKEN")
                }
            }
        }

        publications {
            maven(MavenPublication) {
                from components.java
                afterEvaluate {
                    artifactId = rootProject.name + "-" + it.name
                }
            }
        }
    }
}
